include(ExternalProject)
include(SelectLibraryConfigurations)

set(EXTERNAL_NAME hifiAudioCodec)

string(TOUPPER ${EXTERNAL_NAME} EXTERNAL_NAME_UPPER)

if (WIN32)
  set(DOWNLOAD_URL ${EXTERNAL_HIFI_AUDIO_CODEC_WIN_URLS})
  set(DOWNLOAD_MD5 ${EXTERNAL_HIFI_AUDIO_CODEC_WIN_MD5})
elseif (APPLE)
  set(DOWNLOAD_URL ${EXTERNAL_HIFI_AUDIO_CODEC_MAC_URLS})
  set(DOWNLOAD_MD5 ${EXTERNAL_HIFI_AUDIO_CODEC_MAC_MD5})
elseif (ANDROID)
  set(DOWNLOAD_URL ${EXTERNAL_HIFI_AUDIO_CODEC_ANDROID_URLS})
  set(DOWNLOAD_MD5 ${EXTERNAL_HIFI_AUDIO_CODEC_ANDROID_MD5})
elseif (UNIX)
  set(DOWNLOAD_URL ${EXTERNAL_HIFI_AUDIO_CODEC_LINUX_URLS})
  set(DOWNLOAD_MD5 ${EXTERNAL_HIFI_AUDIO_CODEC_LINUX_MD5})
else()
  return()
endif()

ExternalProject_Add(
  ${EXTERNAL_NAME}
  URL ${DOWNLOAD_URL}
  URL_MD5 ${DOWNLOAD_MD5}
  CONFIGURE_COMMAND ""
  BUILD_COMMAND ""
  INSTALL_COMMAND ""
  LOG_DOWNLOAD 1
)

# Hide this external target (for ide users)
set_target_properties(${EXTERNAL_NAME} PROPERTIES FOLDER "hidden/externals")

ExternalProject_Get_Property(${EXTERNAL_NAME} SOURCE_DIR)

set(${EXTERNAL_NAME_UPPER}_INCLUDE_DIRS ${SOURCE_DIR}/include CACHE STRING INTERNAL)

if (WIN32)
  set(${EXTERNAL_NAME_UPPER}_LIBRARIES ${SOURCE_DIR}/Release/audio.lib CACHE STRING INTERNAL)
else()
  set(${EXTERNAL_NAME_UPPER}_LIBRARIES ${SOURCE_DIR}/Release/libaudio.a CACHE STRING INTERNAL)
endif()
